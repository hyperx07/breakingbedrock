plugins {
    id "com.gradleup.shadow" version "8.3.0"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfig "${mod_id}.mixins.json"
    }

    runs {
        clientMixinDebug {
            client()
            ideConfigGenerated true
            name = "Client Mixin Debug"
            source sourceSets.main
            property 'mixin.debug', 'true'
        }

        serverMixinDebug {
            server()
            ideConfigGenerated true
            name = "Server Mixin Debug"
            source sourceSets.main
            property 'mixin.debug', 'true'
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:$minecraft_version-$forge_version"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

build.dependsOn tasks.register("neoforgeJar", ShadowJar) {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"
    relocate('net.minecraftforge', 'net.neoforged')

    from sourceSets.main.output
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "neoforge"
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set('forge')
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    archiveClassifier.set archiveClassifier.get() + '-forge'
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}